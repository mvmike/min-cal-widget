apply plugin: 'com.android.application'
apply plugin: 'witness'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

android {

    compileSdkVersion 26
    buildToolsVersion "25.0.3"

    defaultConfig {

        applicationId "cat.mvmike.minimalcalendarwidget"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 8
        versionName "0.3.1"

        jackOptions {
            enabled true
        }
    }

    /*
     * To sign release builds, create the file gradle.properties in
     * ~/.gradle/ with this content:
     *
     * signingStoreFile=key.store
     * signingStorePassword=xxx
     * signingKeyAlias=alias
     * signingKeyPassword=xxx
     */
    if (project.hasProperty('signingStoreFile') &&
            project.hasProperty('signingStorePassword') &&
            project.hasProperty('signingKeyAlias') &&
            project.hasProperty('signingKeyPassword')) {

        println "Found sign properties in gradle.properties! Signing buildâ€¦"
        signingConfigs {
            release {
                storeFile file(signingStoreFile)
                storePassword signingStorePassword
                keyAlias signingKeyAlias
                keyPassword signingKeyPassword
            }
        }
        buildTypes.release.signingConfig = signingConfigs.release
    } else {
        buildTypes.release.signingConfig = null
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError true
    }

    compileOptions {
        compileOptions.encoding = "UTF-8"

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// https://developer.android.com/topic/libraries/support-library/packages.html
dependencies {
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:26.0.2'
}

// Only do the libraries imported from maven repositories.
// generate using: `gradle -q calculateChecksums | sort -V`
dependencyVerification {
    verify = [
            'com.android.support:animated-vector-drawable:2a7a8fe5f71be6f96762a1f006ae0efcc2408f58e7279db1103438211107ec56',
            'com.android.support:appcompat-v7:c19ed817942e2472dd42b83c83e74098ff3c1ec33a5997f4422182b1900c1360',
            'com.android.support:support-annotations:32f220ee1c4bfd3dab995548be1a3598993a61e945196d04aa50cec702fa38e4',
            'com.android.support:support-compat:ccfbafada1611360c938be48fa44ecdb53e700745d29fd8d97d6656cb0fda425',
            'com.android.support:support-core-ui:7af6ad3e06a22319001df934bcfa7dfb9d27fe2e969e617d73d9b539ec9efd63',
            'com.android.support:support-core-utils:52bdc2a82d36f33f5565c69b52a6e3d7a5834f039c9ac01bc768ae32290874e0',
            'com.android.support:support-fragment:6217e744dd5750d09378e652f00abf19e0d220c51111901f3228eec815538ad9',
            'com.android.support:support-media-compat:70b5601ee1b8affaee2d39f3c083e4ef9c0c824a67e4d6e5d3d922fc5ea3fdb1',
            'com.android.support:support-v4:b97d6b9b974b243723d56830237649c79124fd9e496396e740f5c6c133142c4f',
            'com.android.support:support-vector-drawable:b4f5793f1ac9a69f9ddc97a763fb4dff8bc1a512251b6cb6cd7d92be6cf3dd14',
    ]
}

pmd {
    toolVersion = '5.5.2'
    consoleOutput = true
}

task pmdMain(type: Pmd, dependsOn: "assembleDebug") {
    ruleSetFiles = files("${project.rootDir}/config/pmd/rules.xml")
    ruleSets = [] // otherwise defaults clash with the list in rules.xml
    source 'src/main/java'
    include '**/*.java'
}

task pmd(dependsOn: [pmdMain]) {}

checkstyle {
    toolVersion = '7.4'
}

task checkstyle(type: Checkstyle) {
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    source 'src/main/java'
    include '**/*.java'

    classpath = files()
}
